{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 203,
   "metadata": {},
   "outputs": [],
   "source": [
    "#Variables\n",
    "R.<x> = ZZ[]\n",
    "C = ComplexField(10000)\n",
    "p = 2\n",
    "n = 1\n",
    "q = p^n\n",
    "dim = 4\n",
    "deg = 2 * dim\n",
    "\n",
    "#Acquired data from the LMFDB\n",
    "#Chnage p,n in variables and below in the loads to your new value of q = p^n, up to q = 5\n",
    "\n",
    "def ConvertLtoWeil(LMFDBlist):\n",
    "    Weilpolys = []\n",
    "    for polynomials in LMFDBlist:\n",
    "        d = polynomials.degree()\n",
    "        newpolycoeffs = []\n",
    "        for i in range(d, -1, -1):\n",
    "            newpolycoeffs.append(polynomials[i])\n",
    "        Weilpolys.append(R(newpolycoeffs))\n",
    "    return Weilpolys\n",
    "\n",
    "#change 2 in the load the below to the new value of q, so that it is load(\"q-weil_polynomials_dim4.sage\")\n",
    "\n",
    "load(\"2-weil_polynomials_dim4.sage\")\n",
    "\n",
    "charpolydim4 = ConvertLtoWeil(data_dim4)\n",
    "charpolydim4prank4 = ConvertLtoWeil(data_dim4_prank4)\n",
    "charpolydim4prank3 = ConvertLtoWeil(data_dim4_prank3)\n",
    "charpolydim4prank2 = ConvertLtoWeil(data_dim4_prank2)\n",
    "charpolydim4prank1 = ConvertLtoWeil(data_dim4_prank1)\n",
    "charpolydim4prank0 = ConvertLtoWeil(data_dim4_prank0)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 175,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "number of Weil polynomials according to function: 1645\n"
     ]
    }
   ],
   "source": [
    "#Build-in function in Sage to generate all q-Weil polynomials of degree deg\n",
    "L = R.weil_polynomials(deg, q)\n",
    "print(\"number of Weil polynomials according to function:\", len(L))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 176,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "1610\n"
     ]
    }
   ],
   "source": [
    "#Generate list of all possible coefficients as in Theorem 10.2.3\n",
    "\n",
    "l = []\n",
    "\n",
    "bounda1lower = math.floor(- 8 * sqrt(q)) + 1\n",
    "bounda1higher = math.ceil(8 * sqrt(q))\n",
    "\n",
    "for a1 in range(bounda1lower, bounda1higher):\n",
    "    u2part1 = 3/8 * a1^2 + 4 * q\n",
    "    \n",
    "    bounda2lower = math.floor(6 * sqrt(q) * abs(a1) - 20 * q) + 1\n",
    "    bounda2higher = math.floor(u2part1) + 1\n",
    "    \n",
    "    for a2 in range(bounda2lower, bounda2higher):\n",
    "        u2 = (-u2part1 + a2)/2\n",
    "        \n",
    "        u3part1 = a1 * a2 / 2 - a1^3 / 8 + a1 * q\n",
    "        bounda3firstvar = (-4/3 * u2)^(3/2)\n",
    "        \n",
    "        bounda3lower1 = math.ceil(u3part1 - bounda3firstvar)\n",
    "        bounda3higher1 = math.floor(u3part1 + bounda3firstvar) + 1\n",
    "        \n",
    "        bounda3secondabs = 4 * sqrt(q) * a2 + 16 * q * sqrt(q)\n",
    "        bounda3secondvar = - 9 * q * a1\n",
    "        \n",
    "        bounda3lower2 = math.floor(- bounda3secondabs + bounda3secondvar) + 1\n",
    "        bounda3higher2 = math.ceil(bounda3secondabs + bounda3secondvar)\n",
    "        \n",
    "        bounda3lower = max(bounda3lower1, bounda3lower2)\n",
    "        bounda3higher = min(bounda3higher1, bounda3higher2)\n",
    "        \n",
    "        for a3 in range(bounda3lower, bounda3higher):\n",
    "            u3 = (u3part1 - a3) / 4\n",
    "            \n",
    "            bounda4abs = 3/256 * a1^4 - a1^2 * a2 / 16 - a1^2 * q / 2 + a1 * a3 / 4 + 2 * a2 * q - 2 * q^2 + 2/3 * u2^2\n",
    "            \n",
    "            etareal = - u2^6 / 27 - 5 * u2^3 * u3^2 + 27/2 * u3^4\n",
    "            etaimaginary = 27/2 * u3 * (- u3^2 - 4/27 * u2^3)^(3/2)\n",
    "            \n",
    "            base = C(etareal, etaimaginary)\n",
    "            eta = base.nth_root(3)\n",
    "            eta2 = eta * E(3)\n",
    "            eta3 = eta * E(3)^2\n",
    "            \n",
    "            root1 = 2 * real_part(eta)\n",
    "            root2 = 2 * real_part(eta2)\n",
    "            root3 = 2 * real_part(eta3)\n",
    "            \n",
    "            bounda4lower1 = math.ceil(bounda4abs + min([root1, root2, root3]))\n",
    "            bounda4higher = math.floor(bounda4abs + median([root1, root2, root3])) + 1\n",
    "            \n",
    "            bounda4lower2 = math.floor(2 * sqrt(q) * abs(a1 * q + a3) - 2 * q * a2 - 2 * q^2) + 1\n",
    "            \n",
    "            bounda4lower = max(bounda4lower1, bounda4lower2)\n",
    "            \n",
    "            for a4 in range(bounda4lower, bounda4higher):\n",
    "                l.append(R([q^4, a1 * q^3, a2 * q^2, a3 * q, a4, a3, a2, a1, 1]))\n",
    "                \n",
    "print(len(l))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 177,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "(x^2 - 2)^2 * (x^2 + 2*x + 2)^2\n",
      "(x^2 + x + 2) * (x^2 + 2*x + 2) * (x^2 - 2)^2\n",
      "(x^2 - 2)^2 * (x^4 + 3*x^3 + 5*x^2 + 6*x + 4)\n",
      "(x^2 - 2)^2 * (x^2 + x + 2)^2\n",
      "(x^2 + 2) * (x^2 + 2*x + 2) * (x^2 - 2)^2\n",
      "(x^2 - 2)^2 * (x^4 + 2*x^3 + 3*x^2 + 4*x + 4)\n",
      "(x^2 - 2)^2 * (x^4 + 2*x^3 + 2*x^2 + 4*x + 4)\n",
      "(x^2 + 2) * (x^2 + x + 2) * (x^2 - 2)^2\n",
      "(x^2 - 2)^2 * (x^4 + x^3 + 3*x^2 + 2*x + 4)\n",
      "(x^2 - x + 2) * (x^2 + 2*x + 2) * (x^2 - 2)^2\n",
      "(x^2 - 2)^2 * (x^4 + x^3 + x^2 + 2*x + 4)\n",
      "(x^2 - 2)^2 * (x^4 + x^3 + 2*x + 4)\n",
      "(x^2 - 2)^2 * (x^4 + x^3 - x^2 + 2*x + 4)\n",
      "(x^2 - 2)^2 * (x^2 + 2)^2\n",
      "(x^2 - x + 2) * (x^2 + x + 2) * (x^2 - 2)^2\n",
      "(x^2 - 2)^2 * (x^4 + 2*x^2 + 4)\n",
      "(x^2 - 2)^2 * (x^4 + x^2 + 4)\n",
      "(x^2 - 2*x + 2) * (x^2 + 2*x + 2) * (x^2 - 2)^2\n",
      "(x^2 - 2)^2 * (x^4 - x^2 + 4)\n",
      "(x^2 - 2)^2 * (x^4 - 2*x^2 + 4)\n",
      "(x^2 - 2)^2 * (x^4 - 3*x^2 + 4)\n",
      "(x^2 - 2)^4\n",
      "(x^2 - x + 2) * (x^2 + 2) * (x^2 - 2)^2\n",
      "(x^2 - 2)^2 * (x^4 - x^3 + 3*x^2 - 2*x + 4)\n",
      "(x^2 - 2*x + 2) * (x^2 + x + 2) * (x^2 - 2)^2\n",
      "(x^2 - 2)^2 * (x^4 - x^3 + x^2 - 2*x + 4)\n",
      "(x^2 - 2)^2 * (x^4 - x^3 - 2*x + 4)\n",
      "(x^2 - 2)^2 * (x^4 - x^3 - x^2 - 2*x + 4)\n",
      "(x^2 - x + 2)^2 * (x^2 - 2)^2\n",
      "(x^2 - 2*x + 2) * (x^2 + 2) * (x^2 - 2)^2\n",
      "(x^2 - 2)^2 * (x^4 - 2*x^3 + 3*x^2 - 4*x + 4)\n",
      "(x^2 - 2)^2 * (x^4 - 2*x^3 + 2*x^2 - 4*x + 4)\n",
      "(x^2 - 2*x + 2) * (x^2 - x + 2) * (x^2 - 2)^2\n",
      "(x^2 - 2)^2 * (x^4 - 3*x^3 + 5*x^2 - 6*x + 4)\n",
      "(x^2 - 2*x + 2)^2 * (x^2 - 2)^2\n",
      "number of polynomials missed by the bounds: 35\n",
      "number of extra polynomials found by the bounds 0\n",
      "number of the missing polynomials that have a real root: 35\n",
      "number of weil polynomials with real roots satisfying the bounds: 0\n"
     ]
    }
   ],
   "source": [
    "#Check for differences between lists generated by function and the one generated by bounds\n",
    "Missing = []\n",
    "\n",
    "for e in L:\n",
    "    if e not in l:\n",
    "        Missing.append(e)\n",
    "        print(e.factor())\n",
    "\n",
    "print(\"number of polynomials missed by the bounds:\", len(Missing))\n",
    "\n",
    "extra = []\n",
    "for f in l:\n",
    "    if f not in L:\n",
    "        extra.append(f)\n",
    "        print(f.factor())\n",
    "\n",
    "print(\"number of extra polynomials found by the bounds\", len(extra))\n",
    "\n",
    "Missingreals = []\n",
    "\n",
    "for pols in Missing:\n",
    "    if (pols(sqrt(q)) == 0) or (pols(- sqrt(q)) == 0):\n",
    "        Missingreals.append(pols)\n",
    "\n",
    "print(\"number of the missing polynomials that have a real root:\", len(Missingreals))\n",
    "\n",
    "realsinresult = []\n",
    "\n",
    "for realtest in l:\n",
    "    if (realtest(sqrt(q)) == 0) or (realtest(- sqrt(q)) == 0):\n",
    "        realsinresult.append(realtest)\n",
    "\n",
    "print(\"number of weil polynomials with real roots satisfying the bounds:\", len(realsinresult))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "metadata": {},
   "outputs": [],
   "source": [
    "#The polynomials above are exactly the ones described in \"the other case\" in Haloui's theorem 1.1"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 197,
   "metadata": {},
   "outputs": [],
   "source": [
    "#check which Weil polynomials are characteristic polynomials of simple abelian varieties over F_q\n",
    "\n",
    "prank0 = []\n",
    "prank1 = []\n",
    "prank2 = []\n",
    "prank3 = []\n",
    "prank4 = []\n",
    "\n",
    "Rp = Zp(p)\n",
    "vp = Rp.valuation()\n",
    "S.<y> = Zp(p, prec = 10000, type = 'capped-rel', print_mode = 'series')[]\n",
    "\n",
    "for polyns in l:\n",
    "    if polyns.is_irreducible():\n",
    "        d = polyns.degree()\n",
    "        vpa4 = vp(polyns[d - 4])\n",
    "        vpa3 = vp(polyns[d - 3])\n",
    "        vpa2 = vp(polyns[d - 2])\n",
    "        vpa1 = vp(polyns[d - 1])\n",
    "        \n",
    "        if vpa4 == 0:\n",
    "            prank4.append(polyns)\n",
    "        else:\n",
    "            rootsof = polyns.roots(Rp)\n",
    "            rootvalus = []\n",
    "            if not len(rootsof) == 0:\n",
    "                for roots in rootsof:\n",
    "                    rootvalus.append(vp(roots[0]))\n",
    "            \n",
    "            coeffsinZp = []\n",
    "            for co in range(polyns.degree() + 1):\n",
    "                coeffsinZp.append(polyns[co])\n",
    "                polynsinZp = S(coeffsinZp)\n",
    "            \n",
    "            factorinZp = polynsinZp.factor()\n",
    "            factordegs = []\n",
    "            for fa in factorinZp:\n",
    "                factordegs.append(fa[0].degree())\n",
    "            \n",
    "            if vpa4 >= n/2 and vpa3 == 0 and n/2 not in rootvalus:\n",
    "                prank3.append(polyns)\n",
    "            \n",
    "            elif vpa4 >= n and vpa3 >= n/2 and vpa2 == 0 and n/2 not in rootvalus:\n",
    "                prank2.append(polyns)\n",
    "            \n",
    "            elif vpa4 == n and vpa3 >= 2/3 * n and vpa2 >= n/3 and vpa1 == 0 and (2/3 * n not in rootvalus) and (n/3 not in rootvalus):\n",
    "                prank1.append(polyns)\n",
    "            \n",
    "            elif vpa4 >= 3/2 * n and vpa3 >= n and vpa2 >= n/2 and vpa1 == 0 and (n/2 not in rootvalus) and 3 not in factordegs:\n",
    "                prank1.append(polyns)            \n",
    "\n",
    "            elif vpa4 == n and vpa3 >= 3/4 * n and vpa2 >= n/2 and vpa1 >= n/4 and len(rootvalus) == 0 and 2 not in factordegs:\n",
    "                prank0.append(polyns) \n",
    "\n",
    "            elif vpa4 >= 3/2 * n and vpa3 == n and vpa2 >= 2/3 *n and vpa1 >= n/3 and len(rootvalus) == 0:\n",
    "                prank0.append(polyns) \n",
    "            \n",
    "            elif vpa4 >= 2 * n and vpa3 >= 3/2 * n and vpa2 >= n and vpa1 >= n/2 and len(rootvalus) == 0 and 3 not in factordegs:\n",
    "                prank0.append(polyns) \n",
    "                \n",
    "\n",
    "    elif len(polyns.factor()) == 1:\n",
    "        if n % 3 == 0 and polyns.factor()[0][1] == 3:\n",
    "            if abs(polyns.factor()[0][0][1]) < 2 * sqrt(q) and polyns.factor()[0][0][1] % q^(1/3) == 0 and not polyns.factor()[0][0][1] / q^(1/3) % p == 0:\n",
    "                prank0.append(polyns)\n",
    "        \n",
    "        elif polyns.factor()[0][1] == 2:\n",
    "            m = polyns.factor()[0][0]\n",
    "            d = m.degree()\n",
    "            b2 = m[d - 2]\n",
    "            b1 = m[d - 1]\n",
    "            \n",
    "            vpb2 = vp(b2)\n",
    "            vpb1 = vp(b1)\n",
    "            \n",
    "            rootsof = m.roots(Rp)\n",
    "            rootvalus = []\n",
    "            if not len(rootsof) == 0:\n",
    "                for roots in rootsof:\n",
    "                    rootvalus.append(vp(roots[0]))\n",
    "            \n",
    "            coeffsinZp = []\n",
    "            for co in range(m.degree() + 1):\n",
    "                coeffsinZp.append(m[co])\n",
    "                polynsinZp = S(coeffsinZp)\n",
    "            \n",
    "            factorinZp = polynsinZp.factor()\n",
    "            factordegs = []\n",
    "            for fa in factorinZp:\n",
    "                factordegs.append(fa[0].degree())\n",
    "            \n",
    "            if vpb2 >= n/2 and vpb1 == 0 and Rp((b2 + 2 * q)^2 - 4 * q * b1).is_square():\n",
    "                prank2.append(polyns)\n",
    "            \n",
    "            elif vpb2 == n/2 and vpb1 >= n/4 and len(rootvalus) == 0:\n",
    "                prank0.append(polyns)\n",
    "                \n",
    "            elif vpb2 >= n and vpb1 >= n/2 and not len(rootvalus) == 0:\n",
    "                prank0.append(polyns)\n",
    "\n",
    "total = prank0 + prank1 + prank2 + prank3 + prank4\n",
    "\n",
    "#beneath is the comparison with LMFDB"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 204,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "number of characteristic polynomials p-rank 4 in LMFDB: 453\n",
      "number of characteristic polynomials p-rank 4 according to code: 453\n",
      "characteristic polynomials in LMFDB but not in code: []\n",
      "characteristic polynomials in code, but not in LMFDB: []\n"
     ]
    }
   ],
   "source": [
    "Missing4 = []\n",
    "\n",
    "for m in charpolydim4prank4:\n",
    "    if m not in prank4:\n",
    "        Missing4.append(m)\n",
    "\n",
    "extra4 = []\n",
    "\n",
    "for M in prank4:\n",
    "    if M not in charpolydim4prank4:\n",
    "        extra4.append(M)\n",
    "\n",
    "print(\"number of characteristic polynomials p-rank 4 in LMFDB:\", len(data_dim4_prank4))\n",
    "print(\"number of characteristic polynomials p-rank 4 according to code:\", len(prank4))\n",
    "print(\"characteristic polynomials in LMFDB but not in code:\", Missing4)\n",
    "print(\"characteristic polynomials in code, but not in LMFDB:\", extra4)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 205,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "number of characteristic polynomials p-rank 3 in LMFDB: 94\n",
      "number of characteristic polynomials p-rank 3 according to code: 94\n",
      "characteristic polynomials in LMFDB but not in code: []\n",
      "characteristic polynomials in code, but not in LMFDB: []\n"
     ]
    }
   ],
   "source": [
    "Missing3 = []\n",
    "\n",
    "for m in charpolydim4prank3:\n",
    "    if m not in prank3:\n",
    "        Missing3.append(m)\n",
    "\n",
    "extra3 = []\n",
    "\n",
    "for M in prank3:\n",
    "    if M not in charpolydim4prank3:\n",
    "        extra3.append(M)\n",
    "\n",
    "print(\"number of characteristic polynomials p-rank 3 in LMFDB:\", len(data_dim4_prank3))\n",
    "print(\"number of characteristic polynomials p-rank 3 according to code:\", len(prank3))\n",
    "print(\"characteristic polynomials in LMFDB but not in code:\", Missing3)\n",
    "print(\"characteristic polynomials in code, but not in LMFDB:\", extra3)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 206,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "number of characteristic polynomials p-rank 3 in LMFDB: 43\n",
      "number of characteristic polynomials p-rank 3 according to code: 43\n",
      "characteristic polynomials in LMFDB but not in code: []\n",
      "characteristic polynomials in code, but not in LMFDB: []\n"
     ]
    }
   ],
   "source": [
    "Missing2 = []\n",
    "\n",
    "for m in charpolydim4prank2:\n",
    "    if m not in prank2:\n",
    "        Missing2.append(m)\n",
    "\n",
    "extra2 = []\n",
    "\n",
    "for M in prank2:\n",
    "    if M not in charpolydim4prank2:\n",
    "        extra2.append(M)\n",
    "\n",
    "print(\"number of characteristic polynomials p-rank 3 in LMFDB:\", len(data_dim4_prank2))\n",
    "print(\"number of characteristic polynomials p-rank 3 according to code:\", len(prank2))\n",
    "print(\"characteristic polynomials in LMFDB but not in code:\", Missing2)\n",
    "print(\"characteristic polynomials in code, but not in LMFDB:\", extra2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 207,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "number of characteristic polynomials p-rank 3 in LMFDB: 32\n",
      "number of characteristic polynomials p-rank 3 according to code: 32\n",
      "characteristic polynomials in LMFDB but not in code: []\n",
      "characteristic polynomials in code, but not in LMFDB: []\n"
     ]
    }
   ],
   "source": [
    "Missing1 = []\n",
    "\n",
    "for m in charpolydim4prank1:\n",
    "    if m not in prank1:\n",
    "        Missing1.append(m)\n",
    "\n",
    "extra1 = []\n",
    "\n",
    "for M in prank1:\n",
    "    if M not in charpolydim4prank1:\n",
    "        extra1.append(M)\n",
    "\n",
    "print(\"number of characteristic polynomials p-rank 3 in LMFDB:\", len(data_dim4_prank1))\n",
    "print(\"number of characteristic polynomials p-rank 3 according to code:\", len(prank1))\n",
    "print(\"characteristic polynomials in LMFDB but not in code:\", Missing1)\n",
    "print(\"characteristic polynomials in code, but not in LMFDB:\", extra1)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 208,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "number of characteristic polynomials p-rank 3 in LMFDB: 43\n",
      "number of characteristic polynomials p-rank 3 according to code: 43\n",
      "characteristic polynomials in LMFDB but not in code: []\n",
      "characteristic polynomials in code, but not in LMFDB: []\n"
     ]
    }
   ],
   "source": [
    "Missing0 = []\n",
    "\n",
    "for m in charpolydim4prank0:\n",
    "    if m not in prank0:\n",
    "        Missing0.append(m)\n",
    "\n",
    "extra0 = []\n",
    "\n",
    "for M in prank0:\n",
    "    if M not in charpolydim4prank0:\n",
    "        extra0.append(M)\n",
    "\n",
    "print(\"number of characteristic polynomials p-rank 3 in LMFDB:\", len(data_dim4_prank0))\n",
    "print(\"number of characteristic polynomials p-rank 3 according to code:\", len(prank0))\n",
    "print(\"characteristic polynomials in LMFDB but not in code:\", Missing0)\n",
    "print(\"characteristic polynomials in code, but not in LMFDB:\", extra0)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 9.3",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.7.10"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
