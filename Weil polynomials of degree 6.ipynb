{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 105,
   "metadata": {},
   "outputs": [],
   "source": [
    "#Variables\n",
    "R.<x> = ZZ[]\n",
    "p = 2\n",
    "n = 1\n",
    "q = p^n\n",
    "dim = 3\n",
    "deg = 2 * dim\n",
    "\n",
    "#Change p,n in variables and below in the loads to your new value of q = p^n, up to q = 9 if you want the whole file to be able to run\n",
    "#can use any q if you want to run everything except the last parts after def CharpolysofsimpleAVofdim3frombounds(l,p,n):\n",
    "\n",
    "#change 2 in the load the below to match the new value of q, so that it is load(\"q-weil_polynomials_dim3.sage\")\n",
    "\n",
    "load(\"2-weil_polynomials_dim3.sage\")\n",
    "\n",
    "#the file that has been loaded contains the characteristic polynomials of all simple abelian varieties over F_q of dimension 3\n",
    "#this data has been downloaded from the LMFDB and the polynomials have been separated based on p-rank\n",
    "#The LMFDB-data uses L-polynomials (which are Weil polynomials, but with the coefficients reversed)\n",
    "#Hence we convert each element in the lists to Weil polynomials\n",
    "\n",
    "def ConvertLtoWeil(LMFDBlist):\n",
    "    Weilpolys = []\n",
    "    for polynomials in LMFDBlist:\n",
    "        d = polynomials.degree()\n",
    "        newpolycoeffs = []\n",
    "        for i in range(d, -1, -1):\n",
    "            newpolycoeffs.append(polynomials[i])\n",
    "        Weilpolys.append(R(newpolycoeffs))\n",
    "    return Weilpolys\n",
    "\n",
    "charpolydim3prank3 = ConvertLtoWeil(data_dim3_prank3)\n",
    "charpolydim3prank2 = ConvertLtoWeil(data_dim3_prank2)\n",
    "charpolydim3prank1 = ConvertLtoWeil(data_dim3_prank1)\n",
    "charpolydim3prank0 = ConvertLtoWeil(data_dim3_prank0)\n",
    "charpolydim3 = charpolydim3prank3 + charpolydim3prank2 + charpolydim3prank1 + charpolydim3prank0\n",
    "\n",
    "#data_dim_3_prankm is the list of data of simple abelian varieties of dimension 3 over F_q with p-rank m"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 106,
   "metadata": {},
   "outputs": [],
   "source": [
    "#Built-in function in Sage to generate all q-Weil polynomials of degree deg\n",
    "L = R.weil_polynomials(deg, q)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 107,
   "metadata": {},
   "outputs": [],
   "source": [
    "#Generate list of all possible coefficients as in Theorem 9.2.3\n",
    "\n",
    "def Weilpolysofdeg6frombounds(q):\n",
    "    l = []\n",
    "\n",
    "    bounda1lower = math.floor(-6 * sqrt(q)) + 1\n",
    "    bounda1higher = math.ceil(6 * sqrt(q))\n",
    "\n",
    "    for a1 in range(bounda1lower, bounda1higher):\n",
    "        bounda2lower = math.floor(4 * sqrt(q) * abs(a1)) - 9 * q + 1\n",
    "        bounda2higher = math.floor(a1^2 / 3 + 3 * q) + 1\n",
    "        \n",
    "        for a2 in range(bounda2lower, bounda2higher):\n",
    "            bounda3lower1 = math.ceil(-2 * a1^3 / 27 + a1 * a2 / 3 + q * a1 - 2 * (a1^2 - 3 * a2 + 9 * q)^(3/2) / 27)\n",
    "            bounda3higher1 = math.floor(-2 * a1^3 / 27 + a1 * a2 / 3 + q * a1 + 2 * (a1^2 - 3 * a2 + 9 * q)^(3/2) / 27) + 1\n",
    "            \n",
    "            bounda3lower2 = math.floor(-2 * q * a1 - 2 * sqrt(q) * a2 - 2 * q * sqrt(q)) + 1\n",
    "            bounda3higher2 = math.ceil(-2 * q * a1 + 2 * sqrt(q) * a2 + 2 * q * sqrt(q))\n",
    "            \n",
    "            bounda3lower = max(bounda3lower1, bounda3lower2)\n",
    "            bounda3higher = min(bounda3higher1, bounda3higher2)\n",
    "            \n",
    "            for a3 in range(bounda3lower, bounda3higher):\n",
    "                coeff = [q^3, q^2 * a1, q * a2, a3, a2, a1, 1]\n",
    "                l.append(R(coeff))\n",
    "\n",
    "    return l\n",
    "\n",
    "l = Weilpolysofdeg6frombounds(q)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 108,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "(x^2 + 2*x + 2) * (x^2 - 2)^2\n",
      "(x^2 + x + 2) * (x^2 - 2)^2\n",
      "(x^2 + 2) * (x^2 - 2)^2\n",
      "(x^2 - x + 2) * (x^2 - 2)^2\n",
      "(x^2 - 2*x + 2) * (x^2 - 2)^2\n",
      "number of polynomials missed by the bounds: 5\n",
      "number of extra polynomials found by the bounds 0\n",
      "number of the missing polynomials that have a real root: 5\n",
      "number of weil polynomials with real roots satisfying the bounds: 0\n"
     ]
    }
   ],
   "source": [
    "#Check for differences between lists generated by the built-in function and the one generated by Theorem 9.2.3\n",
    "def comparisonlists(l,L):\n",
    "    Missing = []\n",
    "\n",
    "    for e in L:\n",
    "        if e not in l:\n",
    "            Missing.append(e)\n",
    "            print(e.factor())\n",
    "\n",
    "    print(\"number of polynomials missed by the bounds:\", len(Missing))\n",
    "\n",
    "    extra = []\n",
    "    for f in l:\n",
    "        if f not in L:\n",
    "            extra.append(f)\n",
    "            print(f.factor())\n",
    "\n",
    "    print(\"number of extra polynomials found by the bounds\", len(extra))\n",
    "\n",
    "    Missingreals = []\n",
    "\n",
    "    for pols in Missing:\n",
    "        if (pols(sqrt(q)) == 0) or (pols(- sqrt(q)) == 0):\n",
    "            Missingreals.append(pols)\n",
    "\n",
    "    print(\"number of the missing polynomials that have a real root:\", len(Missingreals))\n",
    "\n",
    "    realsinresult = []\n",
    "\n",
    "    for realtest in l:\n",
    "        if (realtest(sqrt(q)) == 0) or (realtest(- sqrt(q)) == 0):\n",
    "            realsinresult.append(realtest)\n",
    "\n",
    "    print(\"number of weil polynomials with real roots satisfying the bounds:\", len(realsinresult))\n",
    "\n",
    "comparisonlists(l,L)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 111,
   "metadata": {},
   "outputs": [],
   "source": [
    "#The polynomials missing are exactly the ones with a real root, as described in Theorem 9.2.2 (and 9.2.1 if q is a square)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 109,
   "metadata": {},
   "outputs": [],
   "source": [
    "#check which Weil polynomials are characteristic polynomials of simple abelian varieties over F_q as in Theorem 9.6\n",
    "\n",
    "def CharpolysofsimpleAVofdim3frombounds(l,p,n):\n",
    "\n",
    "    prank0 = []\n",
    "    prank1 = []\n",
    "    prank2 = []\n",
    "    prank3 = []\n",
    "\n",
    "    Rp = Zp(p)\n",
    "    vp = Rp.valuation()\n",
    "    S.<y> = Zp(p, prec = 100, type = 'capped-rel', print_mode = 'series')[]\n",
    "\n",
    "    for polyns in l:\n",
    "        if polyns.is_irreducible():\n",
    "            d = polyns.degree()\n",
    "            vpa3 = vp(polyns[d - 3])\n",
    "            vpa2 = vp(polyns[d - 2])\n",
    "            vpa1 = vp(polyns[d - 1])\n",
    "        \n",
    "            if vpa3 == 0:\n",
    "                prank3.append(polyns)\n",
    "            else:\n",
    "                rootsof = polyns.roots(Rp)\n",
    "                rv = []\n",
    "                if not len(rootsof) == 0:\n",
    "                    for roots in rootsof:\n",
    "                        rv.append(vp(roots[0]))\n",
    "            \n",
    "                coeffsinZp = []\n",
    "                for co in range(polyns.degree() + 1):\n",
    "                    coeffsinZp.append(polyns[co])\n",
    "                    polynsinZp = S(coeffsinZp)\n",
    "            \n",
    "                factorinZp = polynsinZp.factor()\n",
    "                fd = []\n",
    "                for fa in factorinZp:\n",
    "                    fd.append(fa[0].degree())\n",
    "            \n",
    "                if vpa3 >= n/2 and vpa2 == 0 and n/2 not in rv:\n",
    "                    prank2.append(polyns)\n",
    "            \n",
    "                elif vpa3 >= n and vpa2 >= n/2 and vpa1 == 0 and n/2 not in rv:\n",
    "                    prank1.append(polyns)\n",
    "                    \n",
    "                elif vpa3 == n and vpa2 >= 2/3 * n and vpa1 >= n/3 and len(rv) == 0:\n",
    "                    prank0.append(polyns)\n",
    "                \n",
    "                elif vpa3 >= 3/2 * n and vpa2 >= n and vpa1 >= n/2 and len(rv) == 0 and 3 not in fd:\n",
    "                    prank0.append(polyns)            \n",
    "            \n",
    "        elif n % 3 == 0 and len(polyns.factor()) == 1:\n",
    "            if polyns.factor()[0][1] == 3:\n",
    "                omega = polyns.factor()[0][0][1]\n",
    "                if abs(omega) < 2 * sqrt(q) and omega % q^(1/3) == 0:\n",
    "                    if not omega / q^(1/3) % p == 0:\n",
    "                        prank0.append(polyns)\n",
    "\n",
    "    total = prank0 + prank1 + prank2 + prank3\n",
    "\n",
    "    return prank3, prank2, prank1, prank0, total\n",
    "\n",
    "prank3, prank2, prank1, prank0, total = CharpolysofsimpleAVofdim3frombounds(l,p,n)\n",
    "\n",
    "#beneath is the comparison of characteristic polynomials of simple abelian varieties of dimension 3 over F_q with data from LMFDB"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 110,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "number of characteristic polynomials p-rank 3 in LMFDB: 56\n",
      "number of characteristic polynomials p-rank 3 according to code: 56\n",
      "characteristic polynomials in LMFDB but not in code: []\n",
      "characteristic polynomials in code, but not in LMFDB: []\n"
     ]
    }
   ],
   "source": [
    "Missing3 = []\n",
    "\n",
    "for m in charpolydim3prank3:\n",
    "    if m not in prank3:\n",
    "        Missing3.append(m)\n",
    "\n",
    "extra3 = []\n",
    "\n",
    "for M in prank3:\n",
    "    if M not in charpolydim3prank3:\n",
    "        extra3.append(M)\n",
    "\n",
    "print(\"number of characteristic polynomials p-rank 3 in LMFDB:\", len(data_dim3_prank3))\n",
    "print(\"number of characteristic polynomials p-rank 3 according to code:\", len(prank3))\n",
    "print(\"characteristic polynomials in LMFDB but not in code:\", Missing3)\n",
    "print(\"characteristic polynomials in code, but not in LMFDB:\", extra3)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 111,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "number of characteristic polynomials p-rank 2 in LMFDB: 12\n",
      "number of characteristic polynomials p-rank 2 according to code: 12\n",
      "characteristic polynomials in LMFDB but not in code: []\n",
      "characteristic polynomials in code, but not in LMFDB: []\n"
     ]
    }
   ],
   "source": [
    "Missing2 = []\n",
    "\n",
    "for m in charpolydim3prank2:\n",
    "    if m not in prank2:\n",
    "        Missing2.append(m)\n",
    "\n",
    "extra2 = []\n",
    "\n",
    "for M in prank2:\n",
    "    if M not in charpolydim3prank2:\n",
    "        extra2.append(M)        \n",
    "        \n",
    "print(\"number of characteristic polynomials p-rank 2 in LMFDB:\", len(data_dim3_prank2))\n",
    "print(\"number of characteristic polynomials p-rank 2 according to code:\", len(prank2))\n",
    "print(\"characteristic polynomials in LMFDB but not in code:\", Missing2)\n",
    "print(\"characteristic polynomials in code, but not in LMFDB:\", extra2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 112,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "number of characteristic polynomials p-rank 1 in LMFDB: 4\n",
      "number of characteristic polynomials p-rank 1 according to code: 4\n",
      "characteristic polynomials in LMFDB but not in code: []\n",
      "characteristic polynomials in code, but not in LMFDB: []\n"
     ]
    }
   ],
   "source": [
    "Missing1 = []\n",
    "\n",
    "for m in charpolydim3prank1:\n",
    "    if m not in prank1:\n",
    "        Missing1.append(m)\n",
    "\n",
    "extra1 = []\n",
    "\n",
    "for M in prank1:\n",
    "    if M not in charpolydim3prank1:\n",
    "        extra1.append(M)  \n",
    "        \n",
    "print(\"number of characteristic polynomials p-rank 1 in LMFDB:\", len(data_dim3_prank1))\n",
    "print(\"number of characteristic polynomials p-rank 1 according to code:\", len(prank1))\n",
    "print(\"characteristic polynomials in LMFDB but not in code:\", Missing1)\n",
    "print(\"characteristic polynomials in code, but not in LMFDB:\", extra1)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 113,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "number of characteristic polynomials p-rank 0 in LMFDB: 8\n",
      "number of characteristic polynomials p-rank 0 according to code: 8\n",
      "characteristic polynomials in LMFDB but not in code: []\n",
      "characteristic polynomials in code, but not in LMFDB: []\n"
     ]
    }
   ],
   "source": [
    "Missing0 = []\n",
    "\n",
    "for m in charpolydim3prank0:\n",
    "    if m not in prank0:\n",
    "        Missing0.append(m)\n",
    "\n",
    "extra0 = []\n",
    "\n",
    "for M in prank0:\n",
    "    if M not in charpolydim3prank0:\n",
    "        extra0.append(M)  \n",
    "        \n",
    "print(\"number of characteristic polynomials p-rank 0 in LMFDB:\", len(data_dim3_prank0))\n",
    "print(\"number of characteristic polynomials p-rank 0 according to code:\", len(prank0))\n",
    "print(\"characteristic polynomials in LMFDB but not in code:\", Missing0)\n",
    "print(\"characteristic polynomials in code, but not in LMFDB:\", extra0)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 114,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "total number of characteristic polynomials in LMFDB: 80\n",
      "total number of characteristic polynomial according to code: 80\n",
      "characteristic polynomials in LMFDB but not in code: []\n",
      "characteristic polynomials in code but not in LMFDB: []\n"
     ]
    }
   ],
   "source": [
    "Missingtotal = Missing0 + Missing1 + Missing2 + Missing3\n",
    "extratotal = extra0 + extra1 + extra2 + extra3\n",
    "\n",
    "print(\"total number of characteristic polynomials in LMFDB:\", len(charpolydim3))\n",
    "print(\"total number of characteristic polynomial according to code:\", len(total))\n",
    "print(\"characteristic polynomials in LMFDB but not in code:\", Missingtotal)\n",
    "print(\"characteristic polynomials in code but not in LMFDB:\", extratotal)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 9.3",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.7.10"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
